import QtQuick
import Quickshell
import Quickshell.Hyprland
import Quickshell.Io
import "../components"
import "../components/FilterUtils.js" as FilterUtils
import "../services" as Services
import "../emoji/emoji.js" as EmojiData
BasePopup {
    id: root
    anchorMode: "middle"
    implicitWidth: Services.Theme.settingsWidth
    implicitHeight: 16 + Services.Theme.rowHeight + Services.Theme.popupSpacing * 2 + resultGrid.cellHeight * root.gridRows + hint.implicitHeight
    readonly property int gridCols: 10
    readonly property int gridRows: 9
    function escArmed(): bool {
        return !search.hasFocus;
    }
    function quitArmed(): bool {
        return !search.hasFocus;
    }
    Shortcut { sequence: "Down"; enabled: root.visible && !search.hasFocus; onActivated: root.stepSelection(1, root.gridCols) }
    Shortcut { sequence: "Up"; enabled: root.visible && !search.hasFocus; onActivated: root.stepSelection(-1, root.gridCols) }
    Shortcut { sequence: "Left"; enabled: root.visible && !search.hasFocus; onActivated: root.stepSelection(-1, 1) }
    Shortcut { sequence: "Right"; enabled: root.visible && !search.hasFocus; onActivated: root.stepSelection(1, 1) }
    Shortcut { sequence: "Return"; enabled: root.visible && !search.hasFocus; onActivated: root.pick() }
    Shortcut { sequence: "Enter"; enabled: root.visible && !search.hasFocus; onActivated: root.pick() }
    Shortcut { sequence: "/"; enabled: root.visible && !search.hasFocus; onActivated: search.forceFocus() }
    Shortcut { sequence: "j"; enabled: root.visible && !search.hasFocus; onActivated: root.stepSelection(1, root.gridCols) }
    Shortcut { sequence: "k"; enabled: root.visible && !search.hasFocus; onActivated: root.stepSelection(-1, root.gridCols) }
    Shortcut { sequence: "h"; enabled: root.visible && !search.hasFocus; onActivated: root.stepSelection(-1, 1) }
    Shortcut { sequence: "l"; enabled: root.visible && !search.hasFocus; onActivated: root.stepSelection(1, 1) }
    property var entries: []
    property string activeAddress: ""
    property string activeClass: ""
    property string clipTmp: "/tmp/qs-emoji-clip-restore"
    FilterState {
        id: filter
        onRefilterRequested: {
            if (root.visible)
                root.refilter();
        }
    }
    function groupRank(g: int): int {
        if (g < 0)
            return 99;
        if (g <= 1)
            return g;
        if (g === 2)
            return 0;
        if (g <= 9)
            return g - 1;
        return 99;
    }
    function groupName(g: int): string {
        return ["Smileys", "People", "", "Animals", "Food", "Travel", "Activities", "Objects", "Symbols", "Flags"][g] ?? "";
    }
    readonly property string selGroupName: {
        const e = root.entries[resultGrid.currentIndex] ?? null;
        const n = e ? root.groupName(e.g) : "";
        return n !== "" ? n : "Emoji";
    }
    onVisibleChanged: {
        if (visible) {
            Services.EmojiHistory.load();
            search.text = "";
            filter.selMoved = false;
            root.activeAddress = "";
            root.activeClass = "";
            root.pendingEmoji = "";
            focusProbe.running = true;
            root.refilter();
        } else {
            if (focusProbe.running)
                focusProbe.running = false;
        }
    }
    Connections {
        target: Services.EmojiHistory
        function onRecentsChanged(): void {
            if (root.visible)
                root.refilter();
        }
    }
    function stepSelection(dir: int, stride: int): void {
        filter.flush();
        stepListView(resultGrid, dir, stride);
        filter.selMoved = true;
    }
    function refilter(): void {
        const q = String(search.text ?? "").toLowerCase().trim();
        const main = [];
        for (const e of EmojiData.EMOJI) {
            const score = FilterUtils.matchScoreLn(e[2], q);
            if (score < 3)
                main.push({ch: e[0], name: e[1], g: e[3], key: e[0], score: q === "" ? 1 : score});
        }
        main.sort((a, b) => (a.score - b.score) || (root.groupRank(a.g) - root.groupRank(b.g)) || ((a.name < b.name) ? -1 : (a.name > b.name) ? 1 : 0));
        let out = main;
        if (q === "") {
            const seen = {};
            const head = [];
            for (const ch of Services.EmojiHistory.recents) {
                if (seen[ch])
                    continue;
                seen[ch] = true;
                const e = root.entryFor(ch);
                if (e)
                    head.push({ch: ch, name: e[1], g: e[3], key: ch, score: 0});
            }
            out = head.concat(main.filter(m => !seen[m.ch]));
        }
        root.applyResults(out);
    }
    property var emojiByChar: null
    function entryFor(ch: string): var {
        if (!root.emojiByChar) {
            const m = {};
            for (const e of EmojiData.EMOJI)
                m[e[0]] = e;
            root.emojiByChar = m;
        }
        return root.emojiByChar[ch] ?? null;
    }
    function applyResults(out: var): void {
        const idx = filter.keptIndex((root.entries[resultGrid.currentIndex] ?? {}).key ?? "", out);
        root.entries = out;
        if (out.length > 0) {
            resultGrid.currentIndex = Math.min(idx, out.length - 1);
            resultGrid.positionViewAtIndex(resultGrid.currentIndex, GridView.Contain);
        } else {
            resultGrid.currentIndex = -1;
        }
    }
    function pick(): void {
        filter.flush();
        const entry = root.entries[resultGrid.currentIndex];
        if (!entry || saveProbe.running || restoreProbe.running || pasteTimer.running || restoreTimer.running)
            return;
        Services.EmojiHistory.record(entry.ch);
        if (root.activeAddress === "") {
            Services.Notifs.notify({app: "emoji", summary: "No target window", timeout: Services.Theme.osdTimeout});
            return;
        }
        root.pendingEmoji = entry.ch;
        saveProbe.command = ["sh", "-c", 'f="$1"; e="$2"; cliphist list 2>/dev/null | head -n 1 | cut -f1 > "$f.maxid"; rm -f "$f" "$f.type"; if t=$(wl-paste --list-types 2>/dev/null | head -n 1) && [ -n "$t" ]; then printf "%s" "$t" > "$f.type"; wl-paste -t "$t" > "$f" 2>/dev/null || rm -f "$f" "$f.type"; fi; printf "%s" "$e" | wl-copy', "qs", root.clipTmp, entry.ch];
        saveProbe.running = true;
    }
    Process {
        id: saveProbe
        onExited: exitCode => {
            if (exitCode !== 0) {
                Services.Notifs.notify({app: "emoji", summary: "Copy failed", body: "wl-copy failed", timeout: 5000});
                return;
            }
            bar.closePopups();
            pasteTimer.start();
        }
    }
    Timer {
        id: pasteTimer
        interval: Services.Theme.grabDelay
        repeat: false
        onTriggered: root.pasteIntoActive()
    }
    function pasteIntoActive(): void {
        if (root.activeAddress === "")
            return;
        const terminal = /kitty|alacritty|foot|wezterm|ghostty|konsole|gnome-terminal|xfce4-terminal|terminator|tilix|xterm|rxvt|hyper|tabby|stterm|\bst\b/.test(root.activeClass);
        const mods = terminal ? "CTRL, SHIFT" : "CTRL";
        Hyprland.dispatch("hl.dsp.send_shortcut({ mods = \"" + mods + "\", key = \"V\", window = \"address:" + root.activeAddress + "\" })");
        restoreTimer.start();
    }
    Timer {
        id: restoreTimer
        interval: 500
        repeat: false
        onTriggered: {
            restoreProbe.command = ["sh", "-c", 'f="$1"; e="$2"; if [ "$(wl-paste 2>/dev/null)" != "$e" ]; then rm -f "$f" "$f.type" "$f.maxid" "$f.all"; exit 0; fi; if [ -f "$f.type" ] && [ -f "$f" ]; then t=$(cat "$f.type"); case "$t" in text/*|TEXT|STRING|UTF8_STRING) wl-copy < "$f";; *) wl-copy -t "$t" < "$f";; esac 2>/dev/null || wl-copy < "$f"; else wl-copy --clear; fi; sleep 0.3; cliphist list 2>/dev/null > "$f.all"; if [ -s "$f.maxid" ]; then m=$(cat "$f.maxid"); while IFS= read -r line; do id=$(printf "%s\\n" "$line" | cut -f1); case "$id" in ""|*[!0-9]*) continue;; esac; if [ "$id" -gt "$m" ] 2>/dev/null; then printf "%s\\n" "$line" | cliphist delete; fi; done < "$f.all"; else line=$(grep -F -- "$e" "$f.all" 2>/dev/null | tail -n 1); [ -n "$line" ] && printf "%s\\n" "$line" | cliphist delete; fi; rm -f "$f" "$f.type" "$f.maxid" "$f.all"', "qs", root.clipTmp, root.pendingEmoji];
            restoreProbe.running = true;
        }
    }
    property string pendingEmoji: ""
    Process {
        id: restoreProbe
        onExited: exitCode => {
            if (exitCode !== 0)
                Services.Notifs.notify({app: "emoji", summary: "Clipboard restore failed", timeout: 5000});
        }
    }
    Process {
        id: focusProbe
        command: ["sh", "-c", "hyprctl activewindow -j 2>/dev/null | jq -r '[.address,.class] | @tsv' 2>/dev/null"]
        stdout: StdioCollector {
            onStreamFinished: {
                const parts = text.trim().split("\t");
                root.activeAddress = parts[0] ?? "";
                root.activeClass = (parts[1] ?? "").toLowerCase();
            }
        }
    }
    PopupCard {
        SearchField {
            id: search
            catchEscape: true
            onTextChanged: filter.schedule()
            onUpPressed: root.stepSelection(-1, root.gridCols)
            onDownPressed: root.stepSelection(1, root.gridCols)
            onAccepted: root.pick()
            onEscapePressed: search.releaseFocus()
        }
        GridView {
            id: resultGrid
            width: parent.width
            height: cellHeight * root.gridRows
            clip: true
            model: root.entries
            cellWidth: width / root.gridCols
            cellHeight: 42
            onCountChanged: clampListView(resultGrid)
            delegate: Rectangle {
                required property var modelData
                required property int index
                readonly property bool selected: resultGrid.currentIndex === index
                width: GridView.view.cellWidth
                height: GridView.view.cellHeight
                color: selected ? Services.Theme.accent : cellArea.containsMouse ? Services.Theme.hoverBg : Services.Theme.transparent
                MouseArea {
                    id: cellArea
                    anchors.fill: parent
                    hoverEnabled: true
                    cursorShape: Qt.PointingHandCursor
                    onContainsMouseChanged: {
                        if (containsMouse) {
                            resultGrid.currentIndex = index;
                            resultGrid.positionViewAtIndex(index, GridView.Contain);
                            filter.selMoved = true;
                        }
                    }
                    onClicked: {
                        resultGrid.currentIndex = index;
                        filter.selMoved = true;
                        root.pick();
                    }
                }
                Text {
                    anchors.centerIn: parent
                    text: modelData.ch
                    font.family: "Noto Color Emoji"
                    font.pixelSize: 24
                }
            }
        }
        HintText {
            id: hint
            text: root.selGroupName + " · / find · hjkl · ↵ paste · esc close"
        }
    }
}
